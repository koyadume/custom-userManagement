subprojects {
    apply plugin: 'eclipse-wtp'
    apply plugin: 'maven'
    apply plugin: 'java'

    sourceCompatibility = 1.8

    group = 'in.koyad.piston.apps'
    version = computeVersion()
    
    ext {
        servletVersion = '3.1.0'
        jerseyVersion = '2.21'
        pistonVersion = '2.x'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    libsDirName = 'package'

    //required for emulating maven's provided scope as gradle java plugin does not provide this configuration
    configurations {
        provided
        compile.extendsFrom provided
    }
     
    sourceSets {
        main {
            compileClasspath += configurations.provided
        }
        test {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided
        }
    }

    //this is to make sure that runtime dependencies are not shown in eclipse
    eclipse {
        classpath {
            plusConfigurations = [ configurations.compile, configurations.provided ]
        }
    }

    dependencies {
        provided 'org.projectlombok:lombok:1.16.6'
    }

    task syncDependencies(type: Sync) {
        from  configurations.runtime.minus(configurations.provided)
        into 'build/dependencies/lib'
    }
}

def computeVersion() {
	File version = file('version') 
	if(version.exists()) {
		return version.text
	}
    return "1.x"
}

